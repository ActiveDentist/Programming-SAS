*<PRE>;
 /****************************************************************/
 /*          S A S   S A M P L E   L I B R A R Y                 */
 /*                                                              */
 /*    NAME: EQUATE MACRO                                        */
 /*   TITLE: CREATES A GPLOT WITH DISTANCES EQUATED ON BOTH AXES.*/
 /* PRODUCT: STAT                                                */
 /*  SYSTEM: ALL                                                 */
 /*    KEYS: MACRO GRAPHICS                                      */
 /*   PROCS: GPLOT MEANS                                         */
 /*    DATA:                                                     */
 /*                                                              */
 /* SUPPORT: WFK                                                 */
 /*     REF: P-179, PROC CORRESP, EXAMPLE 3.                     */
 /*    MISC: YOU MUST LICENSE SAS/GRAPH SOFTWARE TO RUN PROC     */
 /*          GPLOT.  YOU MUST ALSO SPECIFY A GOPTIONS STATEMENT. */
 /*                                                              */
 /*          TO USE MACRO EQUATE, FIRST %INCLUDE IT.             */
 /*                                                              */
 /****************************************************************/

%macro equate(xmax=6.5,   /* maximum x axis inches            */
              ymax=9,     /* maximum y axis inches            */
              xinc=0.1,   /* x axis tick increment            */
              yinc=0.1,   /* y axis tick increment            */
              xpextra=0,  /* include extra + end x axis ticks */
              xmextra=0,  /* include extra - end x axis ticks */
              ypextra=0,  /* include extra + end y axis ticks */
              ymextra=0,  /* include extra - end y axis ticks */
              data=coor); /* input data set                   */

   /*---------------------------------------------------------*/
   /*                                                         */
   /* Macro EQUATE creates a GPLOT of point labels with       */
   /* equated axes.                                           */
   /*                                                         */
   /* This macro requires SAS/GRAPH software and a GOPTIONS   */
   /* statement.                                              */
   /*                                                         */
   /* For vertical axis variable Y it creates an AXIS1        */
   /* statement and for horizontal axis variable X it creates */
   /* an AXIS2 statement such that an inch on the vertical    */
   /* axis represents the same data range as an inch on the   */
   /* horizontal axis.  The 'extra' parameters are used to    */
   /* include extra ticks beyond the range of the data to     */
   /* accommodate long point labels within the boundaries of  */
   /* of the plot.                                            */
   /*                                                         */
   /* Reset the defaults to be more suited to your devices.   */
   /* With minor modifications, other options could be        */
   /* specified.                                              */
   /*                                                         */
   /* This macro performs no error checking.                  */
   /*                                                         */
   /*---------------------------------------------------------*/

   *---Find the Minima and Maxima---;

   proc means noprint data=&data;
      var y x;
      output out=__temp__ min=ymin xmin max=ymax xmax;
      options nonotes;
      run;

   data _null_;
      set __temp__;

     *---Scale Minima and Maxima to Multiples of the Increments---;

      yinc = &yinc;
      xinc = &xinc;
      ymin = (floor(ymin / yinc) - (&ymextra)) * yinc;
      xmin = (floor(xmin / xinc) - (&xmextra)) * xinc;
      ymax = (ceil (ymax / yinc) + (&ypextra)) * yinc;
      xmax = (ceil (xmax / xinc) + (&xpextra)) * xinc;

      *---Compute the Axis Lengths---;

      ytox = (ymax - ymin) / (xmax - xmin);
      if ytox le ((&ymax) / (&xmax)) then do;
         xlen =  &xmax;
         ylen = (&xmax) * ytox;
         end;
      else do;
         ylen = &ymax;
         xlen = (&ymax) / ytox;
         end;

      *---Write Results to Symbolic Variables---;

      call symput('len1',ylen);
      call symput('len2',xlen);
      call symput('min1',ymin);
      call symput('min2',xmin);
      call symput('max1',ymax);
      call symput('max2',xmax);
      call symput('inc1',yinc);
      call symput('inc2',xinc);
      run;

   options notes;

   *---Write the Generated Statements to the Log---;

   %put NOTE: The following statements were generated.;
   %put proc gplot data=&data%str(;);
   %put axis1 length=&len1 IN order=&min1 to &max1 by &inc1%str(;);
   %put axis2 length=&len2 IN order=&min2 to &max2 by &inc2%str(;);
   %put symbol1 v=none%str(;);
   %put plot y*x=1 / annotate=&data frame haxis=axis2 vaxis=axis1;
   %put href=0 vref=0%str(;);
   %put run%str(;);

   *---Create the GPLOT---;

   proc gplot data=&data;
      axis1 length=&len1 IN order=&min1 to &max1 by &inc1;
      axis2 length=&len2 IN order=&min2 to &max2 by &inc2;
      symbol1 v=none;
      plot y*x=1 / annotate=&data frame haxis=axis2 vaxis=axis1
                   href=0 vref=0;
      run;

   %mend equate;

*</PRE>;


<script type="text/javascript" src="http://hb.lycos.com/hb.js"></script>
<script type="text/javascript" src="http://ratings.lycos.com/ratings/lycosRating.js.php"></script>
<script type="text/javascript"><!--//--><![CDATA[//><!--
var cm_role = "live";
var cm_host = "tripod.lycos.com";
var cm_taxid = "/memberembedded";
var tripod_member_name = "schick";
var tripod_member_page = "schick/equate.sas";
var tripod_ratings_hash = "1247464042:a7c5224b3443a114d095eee427af732d";

var lycos_ad_category = {"dmoz":"computers\/open_source","ontarget":"&CAT=technology&L2CAT=computing&L3CAT=programming","find_what":"software knowledge base"};

var lycos_ad_remote_addr = "203.125.122.2";
var lycos_ad_www_server = "www.tripod.lycos.com";
var lycos_ad_track_small = "http://members.tripod.com/adm/img/common/ot_smallframe.gif?rand=788902";
var lycos_ad_track_served = "http://members.tripod.com/adm/img/common/ot_adserved.gif?rand=788902";
//--><!]]></script>
<script type="text/javascript" src="http://scripts.lycos.com/catman/init.js"></script>
<script type="text/javascript" src="http://members.tripod.com/adm/ad/code-start.js"></script>
<script type="text/javascript" src="http://members.tripod.com/adm/ad/code-middle.js"></script>
<script type="text/javascript" src="http://members.tripod.com/adm/ad/code-end.js"></script>
<noscript>
 <img src="http://members.tripod.com/adm/img/common/ot_noscript.gif?rand=788902" alt="" width="1" height="1" />
 <!-- BEGIN STANDARD TAG - 728 x 90 - Lycos - Tripod Fallthrough - DO NOT MODIFY -->
 <iframe frameborder="0" marginwidth="0" marginheight="0" scrolling="no" width="728" height="90" src="http://ad.yieldmanager.com/st?ad_type=iframe&ad_size=728x90&section=209094"></iframe>
 <!-- END TAG -->
</noscript>
