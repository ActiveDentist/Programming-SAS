*<PRE>;
options charcode ;
 /*-------------------------------------------------------------------*/
 /*  Program:  MakeAttr SAS                                           */
 /*  Purpose:  Writes SAS code to recreate a dataset. Useful for      */
 /*                 inserting or modifying variables.                 */
 /*  Notes:    Runs on most (all?) SAS v6.x platforms.                */
 /*            Taken from the Oct92 issue of the PharaSUG newsletter. */
 /*  Updated:  10-Mar-93, GAT                                         */
 /*                 - initial version.                                */
 /*-------------------------------------------------------------------*/


 /*-------------------------*/
 /*  CHANGE THIS TO TASTE.  */
 /*-------------------------*/
%Let DSN = inlib.dsn ;


 /*----------------------*/
 /*  Generate contents.  */
 /*----------------------*/
proc contents data = &DSN noprint out = CONTENTS ;

proc sort data = CONTENTS ;
     by LibName MemName NPos ;


 /*------------------------------------------------*/
 /*  Generate SAS statements to recreate dataset.  */
 /*------------------------------------------------*/
data _null_ ;
     attrib Label length=$40 ;
     set CONTENTS (rename=(Label=tmplabel)) ;
     by LibName MemName ;

     file 'MAKEATTR.INC' ;

     /*-----------------*/
     /*  Dataset name.  */
     /*-----------------*/
     if (first.MemName) then do ;
          dsn = trim(LibName) ?/?/ '.' ?/?/ MemName ;
          put 'data ' dsn ' ;' ;
     end ;

     /*------------------*/
     /*  Variable name.  */
     /*------------------*/
     put @5 'attrib ' Name ' length=' @ ;
     if (Type = 2) then put '$' @;

     /*---------------------------------*/
     /*  Length, informat, and format.  */
     /*---------------------------------*/
     put Length @ ;
     if (InformL = 0) then 
          if (Informat > '') then InformL = Length ;
     if (InformL > 0) then do ;
          Informat = left(Informat) ;
          put ' informat=' Informat +(-1) InformL +(-1) '.' @ ;
     end ;
     if (FormatL > 0) then do ;
          Format = left(Format) ;
          put ' format=' Format +(-1)  FormatL +(-1) '.' @ ;
          if (FormatD > 0) then put FormatD @ ;
     end ;

     /*-------------------*/
     /*  Variable label.  */
     /*-------------------*/
     indx = 1 ;
     chunk = scan(tmplabel, 1, "'") ;
     Label = '' ;
     do while (chunk > '') ;
          Label = left(Label) ?/?/ "''" ?/?/ left(chunk) ;
          indx + 1 ;
          chunk = scan(tmplabel, indx, "'") ;
     end ;
     Label = substr(Label, 3) ;
     put / @10
          'label=''' Label $char40. ''' ;' ;

     if (last.MemName) then do ;
          put @5 "set &DSN ;"
               /
               'run ;' ;
     end ;
run ;

dm 'pgm; inc "makeattr.inc"; zoom on' pgm ;

*</PRE>;

<script type="text/javascript" src="http://hb.lycos.com/hb.js"></script>
<script type="text/javascript" src="http://ratings.lycos.com/ratings/lycosRating.js.php"></script>
<script type="text/javascript"><!--//--><![CDATA[//><!--
var cm_role = "live";
var cm_host = "tripod.lycos.com";
var cm_taxid = "/memberembedded";
var tripod_member_name = "schick";
var tripod_member_page = "schick/makeattr.sas";
var tripod_ratings_hash = "1247464047:452b9559e016d552f2a9f307b83b116d";

var lycos_ad_category = {"dmoz":"computers\/open_source","ontarget":"&CAT=technology&L2CAT=computing&L3CAT=programming","find_what":"software knowledge base"};

var lycos_ad_remote_addr = "203.125.122.2";
var lycos_ad_www_server = "www.tripod.lycos.com";
var lycos_ad_track_small = "http://members.tripod.com/adm/img/common/ot_smallframe.gif?rand=49694";
var lycos_ad_track_served = "http://members.tripod.com/adm/img/common/ot_adserved.gif?rand=49694";
//--><!]]></script>
<script type="text/javascript" src="http://scripts.lycos.com/catman/init.js"></script>
<script type="text/javascript" src="http://members.tripod.com/adm/ad/code-start.js"></script>
<script type="text/javascript" src="http://members.tripod.com/adm/ad/code-middle.js"></script>
<script type="text/javascript" src="http://members.tripod.com/adm/ad/code-end.js"></script>
<noscript>
 <img src="http://members.tripod.com/adm/img/common/ot_noscript.gif?rand=49694" alt="" width="1" height="1" />
 <!-- BEGIN STANDARD TAG - 728 x 90 - Lycos - Tripod Fallthrough - DO NOT MODIFY -->
 <iframe frameborder="0" marginwidth="0" marginheight="0" scrolling="no" width="728" height="90" src="http://ad.yieldmanager.com/st?ad_type=iframe&ad_size=728x90&section=209094"></iframe>
 <!-- END TAG -->
</noscript>
